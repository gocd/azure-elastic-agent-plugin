/*
 * Copyright 2020 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id 'java'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
  id = 'com.thoughtworks.gocd.elastic-agent.azure'
  pluginVersion = '2.0.0'
  goCdVersion = '22.1.0'
  name = 'GoCD Elastic Agent Plugin for Azure'
  description = 'GoCD Elastic Agent Plugin for Azure allows efficient usage of Azure instances'
  vendorName = 'Thoughtworks, Inc.'
  vendorUrl = 'https://github.com/gocd-private/azure-elastic-agent-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'azure-elastic-agent-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.findByName('jar')]
}

version = gocdPlugin.fullVersion(project)

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

// In this section you declare where to find the dependencies of your project
repositories {
  mavenCentral()
  mavenLocal()
}

ext {
  deps = [
      gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.3.0',
      lombok       : 'org.projectlombok:lombok:1.18.42',
  ]

  versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
  annotationProcessor project.deps.lombok

  compileOnly project.deps.lombok
  compileOnly project.deps.gocdPluginApi

  implementation 'com.google.code.gson:gson:2.13.2'
  implementation 'com.google.guava:guava:33.5.0-jre'
  implementation 'org.apache.commons:commons-lang3:3.19.0'
  implementation 'joda-time:joda-time:2.14.0'

  platform(implementation('com.fasterxml.jackson:jackson-bom:2.20.0'))
  implementation 'com.microsoft.azure:azure:1.41.4'

  constraints {
    implementation('com.nimbusds:oauth2-oidc-sdk:9.43.6') {
      because 'Azure SDK 1.x uses an older version with vulnerable dependencies'
    }
    // Force upgrade transitive dependencies of fabric client to versions without vulnerabilities
    implementation('com.squareup.retrofit2:retrofit:3.0.0') {
      because 'Azure SDK 1.x uses an older version with vulnerable dependencies'
    }
    implementation('com.squareup.okio:okio:1.17.6') {
      because 'Retrofit via Azure SDK uses an older version with vulnerable dependencies'
    }
  }
  implementation 'org.freemarker:freemarker:2.3.34'

  components {
    // workaround for Guava metadata declaring dependencies that are not needed at runtime
    // see https://github.com/google/guava/pull/6606
    withModule('com.google.guava:guava', { details ->
      details.allVariants {
        withDependencies {
          removeAll {
            it.name in [ "listenablefuture", "jspecify", "error_prone_annotations", "j2objc-annotations" ]
          }
        }
      }
    })
  }

  testImplementation platform('org.junit:junit-bom:6.0.0')
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  testImplementation project.deps.gocdPluginApi
  testImplementation 'org.hamcrest:hamcrest:3.0'
  testImplementation platform('org.mockito:mockito-bom:5.20.0')
  testImplementation 'org.mockito:mockito-core'
  testImplementation 'org.mockito:mockito-junit-jupiter'
  testImplementation 'org.skyscreamer:jsonassert:1.5.3'
}

test {
  useJUnitPlatform()
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}
