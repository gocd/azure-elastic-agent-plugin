/*
 * Copyright 2020 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id 'java'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
  id = 'com.thoughtworks.gocd.elastic-agent.azure'
  pluginVersion = '1.2.2'
  goCdVersion = '21.4.0'
  name = 'GoCD Elastic Agent Plugin for Azure'
  description = 'GoCD Elastic Agent Plugin for Azure allows efficient usage of Azure instances'
  vendorName = 'Thoughtworks, Inc.'
  vendorUrl = 'https://github.com/gocd-private/azure-elastic-agent-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'azure-elastic-agent-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.findByName('jar')]
}

version = gocdPlugin.fullVersion(project)

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

// In this section you declare where to find the dependencies of your project
repositories {
  mavenCentral()
  mavenLocal()
}

ext {
  deps = [
      gocdPluginApi: 'cd.go.plugin:go-plugin-api:24.3.0',
      lombok       : 'org.projectlombok:lombok:1.18.34',
  ]

  versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
  annotationProcessor project.deps.lombok

  compileOnly project.deps.lombok
  compileOnly project.deps.gocdPluginApi

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.11.0'
  implementation group: 'com.google.guava', name: 'guava', version: '33.3.1-jre'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
  implementation group: 'joda-time', name: 'joda-time', version: '2.13.0'

  platform(implementation('com.fasterxml.jackson:jackson-bom:2.18.0'))
  implementation group: 'com.microsoft.azure', name: 'azure', version: '1.41.4'

  constraints {
    implementation('com.nimbusds:oauth2-oidc-sdk:9.43.4') {
      because 'Azure SDK 1.x uses an older version with vulnerable dependencies'
    }
    // Force upgrade transitive dependencies of fabric client to versions without vulnerabilities
    implementation('com.squareup.retrofit2:retrofit:2.11.0') {
      because 'Azure SDK 1.x uses an older version with vulnerable dependencies'
    }
    implementation('com.squareup.okio:okio:1.17.6') {
      because 'Retrofit via Azure SDK uses an older version with vulnerable dependencies'
    }
  }
  implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.33'

  components {
    // workaround for Guava metadata declaring dependencies that are not needed at runtime
    // see https://github.com/google/guava/pull/6606
    withModule('com.google.guava:guava', { details ->
      details.allVariants {
        withDependencies {
          removeAll {
            it.name in [ "jsr305", "checker-qual", "error_prone_annotations", "listenablefuture", "j2objc-annotations" ]
          }
        }
      }
    })
  }

  testImplementation platform('org.junit:junit-bom:5.11.1')
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'

  testImplementation project.deps.gocdPluginApi
  testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
  testImplementation platform('org.mockito:mockito-bom:5.13.0')
  testImplementation group: 'org.mockito', name: 'mockito-core'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
}

test {
  useJUnitPlatform()
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}
